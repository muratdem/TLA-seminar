------------------------------ MODULE AwsDNSRace ------------------------------
EXTENDS Naturals, Sequences, TLC

CONSTANTS 
    ENACTORS,             \* set of concurrent Enactors
    PLAN_AGE_THRESHOLD,   \* number of plans after which a plan can be deleted
    MAX_PLAN              \* maximum number of plans (bounded model checking)

VARIABLES 
    current_plan,         \* currently active Route53 plan
    latest_plan,          \* last plan generated by the Planner
    highest_plan_applied, \* highest version ever applied
    plan_deleted,         \* set of deleted plans
    plan_channel,         \* queue of plans awaiting enactment
    enactor_processing,   \* which plan each Enactor is processing
    enactor_pc            \* Enactor program counter

(***************************************************************************)
(* Initial state                                                          *)    
(***************************************************************************)

Init ==
    /\ current_plan = 0
    /\ latest_plan = 0
    /\ highest_plan_applied = 0
    /\ plan_deleted = [i \in 1..MAX_PLAN |-> FALSE]
    /\ plan_channel = << >>
    /\ enactor_processing = [e \in ENACTORS |-> 0]
    /\ enactor_pc = [e \in ENACTORS |-> 0]

(***************************************************************************)
(* Planner behavior                                                        *)
(***************************************************************************)

PlannerStep ==
    /\ latest_plan < MAX_PLAN
    /\ latest_plan' = latest_plan + 1
    /\ plan_channel' = Append(plan_channel, latest_plan + 1)
    /\ UNCHANGED <<current_plan, highest_plan_applied, plan_deleted,
                   enactor_processing, enactor_pc>>

(***************************************************************************)
(* Enactor behavior                                                        *)
(***************************************************************************)

EnactorReceiveStep(self) ==
  /\ plan_channel # << >>
  /\ enactor_pc[self] = 0
  /\ enactor_processing' =
       [enactor_processing EXCEPT ![self] = Head(plan_channel)]
  /\ enactor_pc' =
       [enactor_pc EXCEPT ![self] = 1]       
  /\ plan_channel' = Tail(plan_channel)
  /\ UNCHANGED <<current_plan, latest_plan,
                 highest_plan_applied, plan_deleted>>

EnactorApplyStep(self) ==
    /\ enactor_pc[self] = 1
    /\ IF  ~plan_deleted[enactor_processing[self]]
        THEN
            /\ current_plan' = enactor_processing[self]
            /\ highest_plan_applied' =
                IF enactor_processing[self] > highest_plan_applied
                  THEN enactor_processing[self]
                  ELSE highest_plan_applied
            /\ enactor_pc' =
                [enactor_pc EXCEPT ![self] = 2]     
            /\ UNCHANGED <<latest_plan, plan_deleted, 
                           plan_channel, enactor_processing>>
        ELSE
            /\ enactor_processing' =
                [enactor_processing EXCEPT ![self] = 0]              
            /\ enactor_pc' =
                [enactor_pc EXCEPT ![self] = 0]    
            /\ UNCHANGED <<current_plan, latest_plan, highest_plan_applied,
                           plan_deleted, plan_channel>>

Cleanup(my_plan, old) ==
    [j \in 1..MAX_PLAN |-> 
        IF (my_plan - j >= PLAN_AGE_THRESHOLD) 
           THEN TRUE
           ELSE old[j]
    ]

EnactorCleanupStep(self) ==
    /\ enactor_pc[self] = 2
    /\ plan_deleted' = Cleanup(enactor_processing[self], plan_deleted)
    /\ enactor_processing' =
        [enactor_processing EXCEPT ![self] = 0]
    /\ enactor_pc' =
        [enactor_pc EXCEPT ![self] = 0]                   
    /\ UNCHANGED <<current_plan, latest_plan,
                   highest_plan_applied, plan_channel>>

(***************************************************************************)
(* Next-state relation                                                     *)
(***************************************************************************)
Next ==
    \/ PlannerStep
    \/ \E self \in ENACTORS : EnactorReceiveStep(self)
    \/ \E self \in ENACTORS : EnactorApplyStep(self)
    \/ \E self \in ENACTORS : EnactorCleanupStep(self)



(***************************************************************************)
(* Invariants                                                              *)
(***************************************************************************)

NeverDeleteActive ==
    current_plan > 0 => ~plan_deleted[current_plan]


(***************************************************************************)
(* Specification                                                          *)
(***************************************************************************)

Spec ==
    Init /\ [][Next]_<<current_plan, latest_plan, highest_plan_applied, 
                        plan_deleted, plan_channel, enactor_processing, enactor_pc>>

============================== END MODULE AwsDNSRace ==============================
\* Example constants
\* MAX_PLAN == 5
\* PLAN_AGE_THRESHOLD == 2
\* NUM_ENACTORS == 2